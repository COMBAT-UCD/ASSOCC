globals [
  was-social-distancing-enforced?
]

to-report is-lockdown-enforced?
  if global-confinement-measures = "total-lockdown" [
    set is-lockdown-active? true
    report true
  ]
  if global-confinement-measures = "lockdown-10-5"
  [
    if(ratio-infected > 0.1 or (is-lockdown-active? and ratio-infected > 0.05))
    [
      set is-lockdown-active? true
      report true
    ]
    if ratio-infected <= 0.05 [ set is-lockdown-active? false]
    report false
  ]
  report false
end

to-report closed-non-essential?
  report ratio-infected > ratio-omniscious-infected-that-trigger-non-essential-closing-measure
  or
  #days-trigger-non-essential-business-closing-measure <= current-day
end


to-report closed-schools?
  report (is-closing-school-when-any-reported-case-measure? and is-someone-officially-sick?)
  or
  ratio-infected > ratio-omniscious-infected-that-trigger-school-closing-measure
  or
  #days-trigger-school-closing-measure <= current-day
end

to apply-active-measures
  ask n-of (ratio-population-randomly-tested-daily * count people) people [
    perform-test
  ]

  ask n-of (ratio-population-daily-immunity-testing * count people) people [
    perform-immunity-test
  ]

  if was-social-distancing-enforced? != is-social-distancing-measure-active? [
    set was-social-distancing-enforced? is-social-distancing-measure-active?
    inform-people-of-measures
  ]
end

to-report is-social-distancing-measure-active?
  report ratio-omniscious-infected-that-trigger-social-distancing-measure < ratio-infected
end

to-report people-recorded-by-my-recording-app
  report reduce [[x y] -> (turtle-set x y)] previous-contacts-met
end

to-report average-number-of-people-recorded-by-recording-apps
  report mean [count people-recorded-by-my-recording-app] of people with [is-user-of-tracking-app?]
end

to inform-people-of-measures
  ask n-of (count people * percentage-news-watchers) people [
    set I-know-of-social-distancing? true
    set I-know-of-working-from-home? true
  ]
end

to disable-all-measures
  set ratio-omniscious-infected-that-trigger-school-closing-measure 1
  set ratio-omniscious-infected-that-trigger-social-distancing-measure 1
  set ratio-omniscious-infected-that-trigger-non-essential-closing-measure 1
  set #days-trigger-school-closing-measure 10000
  set #days-trigger-non-essential-business-closing-measure 10000
  set is-closing-school-when-any-reported-case-measure? false
  set global-confinement-measures "none"
  set test-home-of-confirmed-people? false
  set test-workplace-of-confirmed-people? false
  set ratio-population-randomly-tested-daily 0
  set closed-universities? false
  set ratio-of-users-of-the-tracking-app 0
end