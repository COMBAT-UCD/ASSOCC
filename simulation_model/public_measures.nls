globals [
  was-social-distancing-enforced?
  #tests-done-today
  has-2%-infected-threshold-been-met?
  time-start-full-isolation
]

to-report is-lockdown-enforced?
  if global-confinement-measures = "total-lockdown" [
    set is-lockdown-active? true
    report true
  ]
  if global-confinement-measures = "lockdown-10-5"
  [
    if(ratio-infected > 0.1 or (is-lockdown-active? and ratio-infected > 0.05))
    [
      set is-lockdown-active? true
      report true
    ]
    if ratio-infected <= 0.05 [ set is-lockdown-active? false]
    report false
  ]
  report false
end

to-report closed-non-essential?
  report ratio-infected > ratio-omniscious-infected-that-trigger-non-essential-closing-measure
  or
  #days-trigger-non-essential-business-closing-measure <= current-day
end


to-report closed-schools?
  report (is-closing-school-when-any-reported-case-measure? and is-someone-officially-sick?)
  or
  ratio-infected > ratio-omniscious-infected-that-trigger-school-closing-measure
  or
  #days-trigger-school-closing-measure <= current-day
end

to perform-testing
  ; Only test once a day
  if slice-of-the-day = "afternoon" [
    set #tests-done-today 0
    let prioritized-people (turtle-set nobody)

    if prioritize-testing-health-care-and-eduction? [
      set prioritized-people (turtle-set prioritized-people (workers with [[is-hospital?] of my-work]))
      set prioritized-people (turtle-set prioritized-people (workers with [[is-school? or is-university?] of my-work]))
    ]

    ask up-to-n-of #still-available-tests prioritized-people [
      perform-test
    ]

    ; Make sure we do not test people twice
    let people-to-test people with [not member? self prioritized-people]

    if do-not-test-youth? [
      set people-to-test people-to-test with [not is-young?]
    ]

    if only-test-retirees-with-extra-tests? [
      set people-to-test retireds
    ]

    ; We cannot test more people than we have tests available
    let #people-to-test (min (list
      (ratio-population-randomly-tested-daily * count people)
      #still-available-tests))

    ask n-of #people-to-test people-to-test [
      perform-test
    ]

    ask n-of (ratio-population-daily-immunity-testing * count people) people [
      perform-immunity-test
    ]
  ]
end

to apply-active-measures
  perform-testing
  
  if all-self-isolate-for-35-days-when-first-hitting-2%-infected? and ratio-infected > 0.02 and not has-2%-infected-threshold-been-met?
  [
    set has-2%-infected-threshold-been-met? true
    set time-start-full-isolation ticks
    ask people [include-time-for-quarantining 35 * #ticks-per-day]
  ]

  if was-social-distancing-enforced? != is-social-distancing-measure-active? [
    set was-social-distancing-enforced? is-social-distancing-measure-active?
    inform-people-of-measures
  ]
end

to-report #still-available-tests
  report #available-tests - #tests-done-today
end

to-report is-social-distancing-measure-active?
  report ratio-omniscious-infected-that-trigger-social-distancing-measure < ratio-infected
end

to-report people-recorded-by-my-recording-app
  report reduce [[x y] -> (turtle-set x y)] previous-contacts-met
end

to-report average-number-of-people-recorded-by-recording-apps
  report mean [count people-recorded-by-my-recording-app] of people with [is-user-of-tracking-app?]
end

to inform-people-of-measures
  ask n-of (count people * percentage-news-watchers) people [
    set I-know-of-social-distancing? true
    set I-know-of-working-from-home? true
  ]
end

to-report should-be-isolators
  report people with [any? ([gatherers] of my-home) with [is-believing-to-be-infected?]]
end

to disable-all-measures
  ; Closing
  set ratio-omniscious-infected-that-trigger-school-closing-measure 1
  set ratio-omniscious-infected-that-trigger-social-distancing-measure 1
  set ratio-omniscious-infected-that-trigger-non-essential-closing-measure 1
  set #days-trigger-school-closing-measure 10000
  set #days-trigger-non-essential-business-closing-measure 10000
  set is-closing-school-when-any-reported-case-measure? false
  set global-confinement-measures "none"
  set closed-universities? false
  set closed-workplaces? false
  ; Testing
  set ratio-population-randomly-tested-daily 0
  set ratio-population-daily-immunity-testing 0
  set #available-tests 10000
  set test-home-of-confirmed-people? false
  set test-workplace-of-confirmed-people? false
  set prioritize-testing-health-care-and-eduction? false
  set do-not-test-youth? false
  set only-test-retirees-with-extra-tests? false
  ; App settings
  set ratio-of-users-of-the-tracking-app 0
  ; Isolation
  set is-infected-and-their-families-requested-to-stay-at-home? false
  set ratio-self-quarantining-when-a-family-member-is-symptomatic 0
  set food-delivered-to-isolators? false
  set all-self-isolate-for-35-days-when-first-hitting-2%-infected? false
end
