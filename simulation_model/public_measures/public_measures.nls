__includes["public_measures/global_quarantine.nls" "public_measures/tracing_app.nls" "public_measures/testing.nls" "public_measures/school_closing.nls" "public_measures/phasing_out.nls"]

globals [
  was-social-distancing-enforced?
  #tests-done-today
]

to-report is-hard-lockdown-active?
  if global-confinement-measures = "total-lockdown" [
    set is-lockdown-active? true
    report true
  ]
  if global-confinement-measures = "lockdown-10-5"
  [
    if(ratio-infected > 0.1 or (is-lockdown-active? and ratio-infected > 0.05))
    [
      set is-lockdown-active? true
      report true
    ]
    if ratio-infected <= 0.05 [ set is-lockdown-active? false]
    report false
  ]
  report false
end

to-report closed-non-essential?
  if is-equal-to-or-posterior-to-governmental-phase? force-reopening-of-workplaces-after-phase [report false]
  report ratio-infected > ratio-omniscious-infected-that-trigger-non-essential-closing-measure
  or
  #days-trigger-non-essential-business-closing-measure <= current-day
end

to-report closed-workplaces?
  if is-equal-to-or-posterior-to-governmental-phase? force-reopening-of-workplaces-after-phase [report false]
  report close-workplaces?
  or ratio-infected > ratio-omniscious-infected-that-trigger-workplace-closing-measure
end

to-report is-working-from-home-recommended?
  if is-equal-to-or-posterior-to-governmental-phase? force-release-recommendation-working-from-home-after-phase [report false]
  report recommend-working-from-home?
  or ratio-infected > ratio-omniscious-infected-that-trigger-recommendation-working-from-home-measure
end

to-report closed-universities?
  if is-equal-to-or-posterior-to-governmental-phase? force-reopening-of-universities-after-phase [report false]
  report close-universities?
  or ratio-infected > ratio-omniscious-infected-that-trigger-university-closing-measure
end

to-report closed-private-leisure?
  report private-leisure-not-fully-open? and not private-leisure-only-partially-open?
end

to-report private-leisure-not-fully-open?
  if is-equal-to-or-posterior-to-governmental-phase? force-full-reopening-of-private-leisure-after-phase [report false]
  report ratio-infected > ratio-omniscious-infected-that-trigger-private-leisure-closing-measure
end

to-report private-leisure-only-partially-open?
  report private-leisure-not-fully-open? and is-equal-to-or-posterior-to-governmental-phase? force-partial-reopening-of-private-leisure-after-phase
end

to apply-active-measures
  if slice-of-the-day = "morning" [ ; Reset the available tests
    set #tests-done-today 0
  ]
  perform-daily-testing-public-measure
  update-global-quarantine-measure
  update-tracing-app-public-measure

  if was-social-distancing-enforced? != is-social-distancing-measure-active? [
    set was-social-distancing-enforced? is-social-distancing-measure-active?
    inform-people-of-measures
  ]
  
  check-phasing-out
end



to-report #still-available-tests
  report #available-tests - #tests-done-today
end

to-report is-social-distancing-measure-active?
  if is-equal-to-or-posterior-to-governmental-phase? force-releasing-of-social-distancing-measure-after-phase [report false]
  report ratio-omniscious-infected-that-trigger-social-distancing-measure < ratio-infected
end


to inform-people-of-measures
  ask n-of (count people * percentage-news-watchers) people [
    set I-know-of-social-distancing? true
    set I-know-of-working-from-home? true
  ]
end

to-report should-be-isolators
  report people with [is-officially-asked-to-quarantine?]
end

to disable-all-measures
  ; Keep schools, university, workplaces open
  set ratio-omniscious-infected-that-trigger-school-closing-measure 1
  set ratio-omniscious-infected-that-trigger-social-distancing-measure 1
  set ratio-omniscious-infected-that-trigger-non-essential-closing-measure 1
  set ratio-omniscious-infected-that-trigger-workplace-closing-measure 1
  set ratio-omniscious-infected-that-trigger-university-closing-measure 1
  set ratio-omniscious-infected-that-trigger-private-leisure-closing-measure 1
  set ratio-omniscious-infected-that-trigger-recommendation-working-from-home-measure 1
  set ratio-omniscious-infected-that-triggers-retirees-in-isolation 1
  set ratio-omniscious-infected-that-triggers-half-capacity-busses-measure 1
  set #days-trigger-school-closing-measure 10000
  set #days-trigger-non-essential-business-closing-measure 10000
  set is-closing-school-when-any-reported-case-measure? false
  set global-confinement-measures "none"
  set close-universities? false
  set close-workplaces? false
  ; Disable testing
  set ratio-population-randomly-tested-daily 0
  set ratio-population-daily-immunity-testing 0
  set #available-tests 10000
  set test-home-of-confirmed-people? false
  set test-workplace-of-confirmed-people? false
  set prioritize-testing-health-care? false
  set prioritize-testing-education? false
  set do-not-test-youth? true
  set only-test-retirees-with-extra-tests? false
  set when-is-daily-testing-applied? "never"
  ; Disable tracing (Smartphone App)
  set ratio-of-people-using-the-tracking-app 0
  ; Disable isolation
  set is-infected-and-their-families-requested-to-stay-at-home? false
  set food-delivered-to-isolators? false
  set is-psychorigidly-staying-at-home-when-quarantining? false
  set ratio-self-quarantining-when-symptomatic 0
  set ratio-self-quarantining-when-a-family-member-is-symptomatic 0
  set recommend-working-from-home? false
  
  disable-global-quarantine-measure
  disable-tracing-app-measure
  disable-daily-testing-measure
  disable-phasing-out-measure
end
