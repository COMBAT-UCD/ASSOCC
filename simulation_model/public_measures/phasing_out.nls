globals [
  current-governmental-model-phase
  day-of-last-phase-shift
]

to setup-phasing-out
  set current-governmental-model-phase crisis-not-acknowledged-phasing-out-state
end

to setup-measures-for-phasing-out
  if condition-for-acknowledging-the-crisis = "ratio infected>acknowledgement-ratio" [
    ; We need to make sure that the measures that we want to loosen also actually trigger, because otherwise the system will not actually work
    if force-reopening-of-schools-after-phase != "never" [
      set ratio-omniscious-infected-that-trigger-school-closing-measure acknowledgement-ratio
    ]
    if force-reopening-of-non-essential-after-phase != "never" [
      set ratio-omniscious-infected-that-trigger-non-essential-closing-measure acknowledgement-ratio
    ]
    if force-reopening-of-workplaces-after-phase != "never" [
      set ratio-omniscious-infected-that-trigger-workplace-closing-measure acknowledgement-ratio
    ]
    if force-release-recommendation-working-from-home-after-phase != "never" [
      set ratio-omniscious-infected-that-trigger-recommendation-working-from-home-measure acknowledgement-ratio
    ]
    if force-reopening-of-universities-after-phase != "never" [
      set ratio-omniscious-infected-that-trigger-university-closing-measure acknowledgement-ratio
    ]
    if force-full-reopening-of-private-leisure-after-phase != "never" [
      set ratio-omniscious-infected-that-trigger-private-leisure-closing-measure acknowledgement-ratio
    ]
    if force-releasing-of-social-distancing-measure-after-phase != "never" [
      set ratio-omniscious-infected-that-trigger-social-distancing-measure acknowledgement-ratio
    ]
    if force-releasing-retirees-from-isolation-after-phase != "never" [
      set ratio-omniscious-infected-that-triggers-retirees-in-isolation acknowledgement-ratio
    ]
    if force-releasing-retirees-from-isolation-after-phase != "never" [
      set ratio-omniscious-infected-that-triggers-half-capacity-busses-measure acknowledgement-ratio
    ]
  ]
end

to check-phasing-out
  if current-governmental-model-phase = crisis-not-acknowledged-phasing-out-state [
   if condition-for-acknowledging-the-crisis = "ratio infected>acknowledgement-ratio" and ratio-infected > acknowledgement-ratio [
      set current-governmental-model-phase ongoing-crisis-governmental-model-phase
      stop
    ]
    ;error (sentence "not implemented for" current-governmental-model-phase condition-phasing-out)
  ]

  if current-governmental-model-phase = ongoing-crisis-governmental-model-phase and current-day - day-of-last-phase-shift >= minimum-days-between-phases [
    if can-go-to-next-phase? [
      set current-governmental-model-phase phase-1-phasing-out-state
      set day-of-last-phase-shift current-day
      stop
    ]
    ;error (sentence "not implemented for" current-governmental-model-phase condition-phasing-out)
  ]

  if current-governmental-model-phase = phase-1-phasing-out-state and current-day - day-of-last-phase-shift >= minimum-days-between-phases [
    if can-go-to-next-phase? [
      set current-governmental-model-phase phase-2-phasing-out-state
      set day-of-last-phase-shift current-day
      stop
    ]
  ]
end

to-report can-go-to-next-phase?
  if condition-phasing-out = "35 days of quarantine"
  [report is-number? start-tick-of-global-quarantine and (ticks - start-tick-of-global-quarantine) > 35 * #ticks-per-day]

  if condition-phasing-out = "#infected has decreased in day gap"
  [report (item (ticks - day-gap-for-phasing-out-condition * #ticks-per-day) #infected-people-per-ticks) > (item (ticks - 1) #infected-people-per-ticks) ]

  if condition-phasing-out = "hospital not overrun & #hospitalizations has decreased in day gap"
  [report (item (ticks - day-gap-for-phasing-out-condition * #ticks-per-day) #hospitalized-per-tick) > (item (ticks - 1) #hospitalized-per-tick)]

  if condition-phasing-out = "percentage immune"
  [report ratio-immune > next-phase-condition-percentage]

  if condition-phasing-out = "percentage deceased"
  [report ratio-deceased > next-phase-condition-percentage]

  if condition-phasing-out = "new infections percentage of average over day gap" [
    let #newly-infected-comparison-period sublist #newly-infected-per-ticks (max (list (length #newly-infected-per-ticks - day-gap-for-phasing-out-condition * #ticks-per-day) 0)) (length #newly-infected-per-ticks)
    let #newly-infected-per-day sum-ticks-to-days #newly-infected-comparison-period
    let average-new-infections mean (but-last #newly-infected-per-day)
    report ((last #newly-infected-per-day) / average-new-infections) < next-phase-condition-percentage
  ]

  error (word "Unrecognized phasing out condition: " condition-phasing-out)
end

to-report phase-1-phasing-out-state
  report "phase-1"
end

to-report phase-2-phasing-out-state
  report "phase-2"
end

to-report ongoing-crisis-governmental-model-phase
  report "ongoing crisis"
end

to-report crisis-not-acknowledged-phasing-out-state
  report "crisis-not-acknowledged"
end

to-report crisis-is-acknowledged?
  report current-governmental-model-phase != "crisis-not-acknowledged"
end

to-report is-equal-to-or-posterior-to-governmental-phase? [phase]
  if phase = "never" [report false]
  if phase = phase-1-phasing-out-state [report current-governmental-model-phase = phase-1-phasing-out-state or is-equal-to-or-posterior-to-governmental-phase? phase-2-phasing-out-state]
  if phase = phase-2-phasing-out-state [report current-governmental-model-phase = phase-2-phasing-out-state]
  report false
end

to disable-phasing-out-measure
  set condition-for-acknowledging-the-crisis "never"
  set force-reopening-of-schools-after-phase "never"
  set force-reopening-of-non-essential-after-phase "never"
  set force-reopening-of-workplaces-after-phase "never"
  set force-release-recommendation-working-from-home-after-phase "never"
  set force-reopening-of-universities-after-phase "never"
  set force-partial-reopening-of-private-leisure-after-phase "never"
  set force-full-reopening-of-private-leisure-after-phase "never"
  set force-releasing-of-social-distancing-measure-after-phase "never"
  set force-releasing-retirees-from-isolation-after-phase "never"
  set force-full-bus-capacity-after-phase "never"
end
