;;Satisfaction undiscounted expected functions

;youth do not have capital because they do not participate in the economic model

to-report SUE-financial-safety-transport [mt]
  if age != "young" [
    if my-previous-amount-of-capital <= 0 [
      report -1
    ]
    if mt = "solo" [
      report (- (solo-transport-costs / my-previous-amount-of-capital))
    ]
    if mt = "public" [
      report (- (public-transport-costs / my-previous-amount-of-capital))
    ]
    if mt = "shared-car" [
      report (- (car-sharing-costs / my-previous-amount-of-capital))
    ]
  ]
  report 0
end

to-report SUE-financial-survival-transport [mt]
  if age != "young" [
    let eating-costs price-of-rations-in-essential-shops * amount-of-rations-I-buy-when-going-to-essential-shops
    if mt = "solo" [
      report (- min (list ((my-amount-of-capital - solo-transport-costs) / eating-costs) 1))
    ]
    if mt = "public" [
      report (- min (list ((my-amount-of-capital - public-transport-costs) / eating-costs) 1))
    ]
    if mt = "shared-car" [
      report (- min (list ((my-amount-of-capital - car-sharing-costs) / eating-costs) 1))
    ]
  ]
  report 0
end

to-report SUE-conformity-transport [mt]
  if length what-my-network-did >= 28 [
    if mt = first what-transport-my-network-used [report 0.4]
    report 0
  ]
  report 1
end

to-report SUE-risk-avoidance-transport [mt]
  let result 0.1
  if mt = "solo" [set result 0.2]
  if mt = "public" [
      if #max-people-per-bus = 0 [set result 0.2 ]
      if #max-people-per-bus < 5 [set result 0.1 ]
      if #max-people-per-bus < 10 [set result 0.05 ]
      ifelse #max-people-per-bus < 20 [set result 0]
      [set result -0.1]
  ]
  if mt = "shared-car" [set result 0.1]

  ;ifelse social-distancing-of ad
  ;  [ set result result + 0.1 ]
  ;  [ set result result - 0.1 ]

  report result
end

;;Satisfaction discounted expected functions
to-report SDE-financial-safety-transport [mt]
  let expected-increase SUE-financial-safety-transport mt
  let discounted-increase expected-increase * (1 - financial-safety-satisfaction-level)
  report discounted-increase
end

to-report SDE-financial-survival-transport [mt]
  let expected-increase SUE-financial-survival-transport mt
  let discounted-increase expected-increase * (1 - financial-survival-satisfaction-level)
  report discounted-increase
end

to-report SDE-conformity-transport [mt]
  let expected-increase SUE-conformity-transport mt
  let discounted-increase expected-increase * (1 - conformity-satisfaction-level)
  report discounted-increase
end

to-report SDE-risk-avoidance-transport [mt]
  let expected-increase SUE-risk-avoidance-transport mt
  let discounted-increase expected-increase * (1 - risk-avoidance-satisfaction-level)
  report discounted-increase
end

;;Satisfaction  actual functions
to-report SDA-financial-safety-transport [mt]
  report SDE-financial-safety-transport mt
end

to-report SDA-financial-survival-transport [mt]
  report SDE-financial-survival-transport mt
end


to-report SUA-conformity-transport [mt]
  if length what-my-network-did >= 28 [
    if mt = last what-transport-my-network-used [report 0.4]
    report 0
  ]
  report 1
end

to-report  SDA-conformity-transport [mt]
  let expected-increase SUA-conformity-transport mt
  let discounted-increase expected-increase * (1 - conformity-satisfaction-level)
  report discounted-increase
end

;to be updated based on the SDA function for actions
to-report SDA-risk-avoidance-transport [mt]
  report SDE-risk-avoidance-transport mt
end


;;Habit
to-report add-occurance [occ occ-list]
  let index 0
  foreach occ-list [occ-count -> 
    if occ = item 0 occ-count [
      report replace-item index occ-list (list (item 0 occ-count) (item 1 occ-count + 1))
    ]
    set index index + 1
  ]
  report lput (list occ 1) occ-list
end

to-report determine-my-preferred-means-of-transport
  let counts-of-my-means-of-transport []
  foreach remove "stay-here" what-transport-I-used [mt ->
    set counts-of-my-means-of-transport add-occurance mt counts-of-my-means-of-transport
  ]
  let my-preferred-means-of-transport (item 0 (reduce [ [x y] -> ifelse-value item 1 x > item 1 y [x] [y] ] counts-of-my-means-of-transport))
  report my-preferred-means-of-transport
end

to-report report-habit-satisfaction [mt]
  if not empty? remove "stay-here" what-transport-I-used [
    if length what-transport-I-used >= 28 [
      let my-preferred-means-of-transport determine-my-preferred-means-of-transport
      if my-preferred-means-of-transport = mt [
        report 1
      ]
      report 0
    ]
    report 1
  ]
  report 0
end

;;Prevalence calculation
to-report expected-global-prevalence-of [mt]
  ;reset-timer
  let total-satisfaction 0
  ;if cultural model is activated then:
  ;calculate the total-satisfaction an agent expects to gain from a means of transport as the weighted sum of all its needs
  set total-satisfaction sum (list
    ;Needs
    (SDE-financial-safety-transport mt * importance-weight-financial-safety) 
    (SDE-financial-survival-transport mt * importance-weight-financial-survival)
    (SDE-conformity-transport mt * importance-weight-conformity)
    (SDE-risk-avoidance-transport mt * importance-weight-risk-avoidance)
    
    ;Further factors
    (report-habit-satisfaction mt)
    
    ;Calibration probability
    (my-probability-of-taking mt)
  )
  report total-satisfaction
end


to-report determine-means-of-transport
  let means-of-transport ["solo" "public" "shared-car"]
  report item 0 reduce [[x y] -> ifelse-value item 1 x > item 1 y [x] [y]] (map [x -> (list x (expected-global-prevalence-of x))] means-of-transport)
end