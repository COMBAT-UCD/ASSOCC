to execute-activity-effect
  ifelse activity-model = "public&private leisure, rest, age-based work"
  [
    if [gathering-type] of current-activity = "essential-shop"
    [ set has-done-shopping true ]
    ;might be interesting to hold this variable as an integer, for storing when the next shopping will be needed
    ;additionally, we might want at some point to hold additional variables within homes (i.e. one person getting the food for the whole community)
    
    ;the epistemic infection status depends on whether the person has (had) visible symptoms, not on where it now gathers
    ;though, gathering place might be to be recorded (i.e. a "maybe infected" state if public space has been visited and public space is believed to be contagious)
    ;if [gathering-type] of current-activity = "hospital"
    ;[ set epistemic-infection-status  infection-status ]
  ]
  [error "not implemented"]
end

to perform-activity
  set shape "default"
  ifelse activity-model = "public&private leisure, rest, age-based work" and age-model = "young,student,worker,retired"
  [
    let target nobody
    let motivation ""
    
    if (slice-of-the-day = "morning" or slice-of-the-day = "afternoon") and working-day
    [
      if age = "young" [set target one-of link-neighbors with [gathering-type = "school"] set motivation "mandatory" ]
      if age = "student" [set target one-of link-neighbors with  [gathering-type = "university"] set motivation "learning" ]
      if age = "worker" [set target
        [other-end] of one-of my-gathering-links with  [member? "contractual obligation" features]
        set motivation "contractual obligation"
      ]
      if age = "retired" [set target one-of link-neighbors with  [gathering-type = "home"] set motivation "rest"]
    ]
    if slice-of-the-day = "evening" or not working-day
    [
      if not has-done-shopping and (day-of-the-week = "sunday" or random-float 1 < 0.15) [
        set target [other-end] of one-of my-gathering-links with [member? "essential shopping" features]
        set motivation "essential shopping"
      ]
      if target = nobody and random-float 1 < 0.05 [set target one-of link-neighbors with [gathering-type = "public-leisure"] set motivation "relaxing" ]
      if target = nobody and random-float 1 < 0.05 [set target one-of link-neighbors with [gathering-type = "non-essential-shop"] set motivation "shopping" ]
      if target = nobody and random-float 1 < 0.3 [set target one-of link-neighbors with [gathering-type = "private-leisure"] set motivation "relaxing" ]
      if target = nobody [set target one-of link-neighbors with  [gathering-type = "home"] set motivation "rest" ]
    ]
    if random-float 1 < 0.01 [
      set target [other-end] of one-of my-gathering-links with  [member? "get-tested" features]
      set motivation "get-tested"
    ]
    
    if is-lockdown-enforced? [
      if not [practicable-at-home?] of target [
        set motivation "rest"
      ]
      set target one-of link-neighbors with [gathering-type = "home"]
    ]
    
    if target != current-activity[
      move-to one-of [patches in-radius 3  with [pxcor > [pxcor] of myself]] of target
      set heading towards target
    ]
    set current-activity target
    set current-motivation motivation
    execute-activity-effect
  ]
  [error "not implemented"]
end