__includes ["people_activity.nls" "people_age_model.nls" "disease_model.nls"]

people-own [age infection-status current-activity]

to setup-person
  setup-person-age
  
  set color default-color
  set infection-status "healthy"
  
  relate-person-to-activities
end

to-report default-color
  if age-model = "young-old"
  [
    ifelse age = "young" [report green]
    [report blue]
  ]

  if age-model = "young,student,worker,retired"
  [
    if age = "young" [report green]
    if age = "student" [report orange]
    if age = "worker" [report blue]
    if age = "retired" [report red]
  ]
  error "not implemented"
end

to update-people-display
  set color default-color
  if infection-status = "healthy"
  [set color color + 2]
  if infection-status = "infected"
  [set color color ]
  if infection-status = "dead"
  [set color color - 2]
  if infection-status = "immune"
  [set color white]
 
end

to set-infection-status [infection]
  set infection-status infection
end



to-report is-immune?
  if disease-model = "markovian" [report infection-status = "immune"]
  error "not implemented"
end

to update-within-disease-status
  ifelse disease-model = "markovian" 
  [
    if infection-status = "infected"
    [
      let immune? false
      let dead? false
      set immune?  random-float 1 < probability-recovering-mdm
      set dead?  random-float 1 < probability-passing-out-mdm
      
      if immune? [set infection-status "immune"]
      if dead? [set infection-status "dead"]
    ]
  ]
  [error "not implemented"]
end



to create-all-people
  if original-distribution = "one-person-per-patch"
  [ask patches [sprout-people 1 [setup-person]]]
  if original-distribution = "set-by-quotas"
  [create-people #young + #students + #workers + #retired [setup-person]]
  
  ask people [ set current-activity one-of link-neighbors with  [activity-type = "home"]]
  
  
end

to relate-person-to-activities
  create-link-to one-of activities with [activity-type = "home"]
  ifelse activity-model = "public&private leisure, rest, age-based work" and age-model = "young,student,worker,retired"
  [
  create-link-to one-of activities with [activity-type = "private-leisure"]
  create-link-to one-of activities with [activity-type = "public-leisure"]
  
  if age = "young" [ create-link-to one-of activities with [activity-type = "school"]]
  if age = "student" [ create-link-to one-of activities with [activity-type = "university"]]
  if age = "worker" [ create-link-to one-of activities with [activity-type = "workplace"]]
  ]
  [error "not implemented"]
end