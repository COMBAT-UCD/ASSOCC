__includes ["people_activity.nls" "people_age_model.nls" "disease_model.nls"]

people-own [age infection-status current-activity]

to setup-person
  setup-person-age
  relate-person-to-activities
  set color default-color
  set infection-status "healthy"  
end

to-report default-color
  if age-model = "young-old"
  [
    ifelse age = "young" [report green]
    [report blue]
  ]

  if age-model = "young,student,worker,retired"
  [
    if age = "young" [report green]
    if age = "student" [report orange]
    if age = "worker" [report blue]
    if age = "retired" [report red]
  ]
  error "not implemented"
end

to update-people-display
  set color default-color
  if infection-status = "healthy"
  [set color color + 2]
  if infection-status = "infected"
  [set color color ]
  if infection-status = "dead"
  [set color color - 2]
  if infection-status = "immune"
  [set color white]
 
end

to set-infection-status [infection]
  set infection-status infection
end





to create-all-people
  if original-distribution = "one-person-per-patch"
  [ask patches [sprout-people 1 [setup-person]]]
  if original-distribution = "set-by-quotas"
  [create-people #young + #students + #workers + #retired [setup-person]]
  
  ask people [ set current-activity one-of link-neighbors with  [gathering-type = "home"]]
end

to relate-person-to-activities
  create-gathering-link-to one-of gathering-points with [gathering-type = "home"] 
  [
    set commitment 1
    set motives-for-joining (list "rest" "visit family") 
  ]
  ifelse activity-model = "public&private leisure, rest, age-based work" and age-model = "young,student,worker,retired"
  [
    create-gathering-link-to one-of gathering-points with [gathering-type = "private-leisure"]
    [ set commitment 1 set motives-for-joining (list "relaxing")]
    create-gathering-link-to one-of gathering-points with [gathering-type = "public-leisure"]
    [ set commitment 1 set motives-for-joining (list "relaxing")]
    
    if age = "young" [ create-gathering-link-to one-of gathering-points with [gathering-type = "school"]
      [ set commitment 1
        set motives-for-joining (list "mandatory")]
    ]
    
    if age = "student" [ create-gathering-link-to one-of gathering-points with [gathering-type = "university"]
      [ set commitment 1 set motives-for-joining (list "learning")]]
    
    if age = "worker" [ create-gathering-link-to one-of gathering-points with [gathering-type = "workplace"]
      [ set commitment 1 set motives-for-joining (list "getting money" "contractual obligation")]
    ]
  ]
  [error "not implemented"]
end