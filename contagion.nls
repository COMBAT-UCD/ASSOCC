to spread-contagion  
  spread-by-activity-proximity
end

to spread-by-activity-proximity
  ask people [
    if is-away? [apply-risk-of-contamination-when-away]
    apply-risk-of-contamination-when-travelling
    
    let number-contamination count (other [link-neighbors] of current-activity) with [current-activity = [current-activity] of myself and is-contagious?]
    repeat number-contamination [ apply-risk-of-contamination [proximity-factor] of current-activity * ifelse-value is-young?  [factor-reduction-probability-transmission-young] [1]]
  ]
end

to apply-risk-of-contamination-when-travelling
  if density-travelling-propagation = 0 [stop]
  let travellers people with [is-moving-outside-for-reaching-current-activity?]
  let #contagious count travellers with [is-contagious?]
  
  ;;could be fastened with a bit of math, like 1-(1-p)^k
  repeat (#contagious) [
    apply-risk-of-contamination density-travelling-propagation * ifelse-value is-young? [factor-reduction-probability-transmission-young] [1]]
end

to apply-risk-of-contamination-when-away
  if random-float 1 < probability-infection-when-abroad * (ifelse-value is-young? [factor-reduction-probability-transmission-young] [1]) and infection-status = "healthy"
  [
    set-contaminated
  ]
end

to set-contaminated
  set infection-status "asymptomatic noncontagious" 
  set duration-current-disease-status infection-to-asymptomatic-contagiousness
end

to apply-risk-of-contamination [factor]
  let contagion-occurs? false
  if (not is-away? and random-float 1 < factor * propagation-risk and infection-status = "healthy")
  [set contagion-occurs? true]
  
  if contagion-occurs?
  [
    set-contaminated
  ]
end

to-report is-contagious?
  report not (infection-status = "healthy") and not (infection-status = "immune")
end

to-report is-alive?
  report not (infection-status = "dead")
end

to infect-one-random-person
    ask n-of (min list 3 count people) people [
    set-infection-status "infected-asymptomatic"  set duration-current-disease-status infection-to-asymptomatic-contagiousness
  ]
end

